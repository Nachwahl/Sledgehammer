plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

def versionDir = "${buildDir}/generated-version"

allprojects {
    apply plugin: "java"

    ext {
        sledgehammerVersion = "0.5.0." + ('git rev-list --count HEAD'.execute().text.trim()) + "-1.12.2"

        husbyVersion = "3c6ad811b2"
        guavaVersion = "30.1.1-jre"
        lombokVersion = "1.18.20"
    }

    group "com.noahhusby"
    version "$sledgehammerVersion"

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
        maven { url 'https://repo.codemc.io/repository/maven-snapshots' }
        maven { url 'https://jitpack.io/' }
        maven { url 'https://maven.daporkchop.net/'}
    }

    dependencies {
        if (!("common".equalsIgnoreCase(project.name))) {
            implementation project(":common")
        }

        implementation "com.github.noahhusby.HusbyLib:data:$husbyVersion"

        compileOnly "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }
}

dependencies {
    for (Project prj : project.allprojects) {
        if (!"sledgehammer".equalsIgnoreCase(prj.name) && prj.parent.name.equalsIgnoreCase("sledgehammer")) {
            implementation project(":" + prj.name)
        }
    }
}

sourceSets {
    main {
        output.dir(versionDir, builtBy: 'generateVersionProperties')
    }
}

task generateVersionProperties {
    doLast {
        def propertiesFile = file "$versionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty("version", sledgehammerVersion.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}

build.dependsOn shadowJar
processResources.dependsOn generateVersionProperties
