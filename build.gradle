plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'net.linguica.maven-settings' version '0.5'
}

def versionDir = "${buildDir}/generated-version"

repositories {
    mavenCentral()
}

allprojects {
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "java"
    apply plugin: "net.linguica.maven-settings"

    ext {
        sledgehammerVersion = "0.4.3." + ('git rev-list --count HEAD'.execute().text.trim()) + "-1.12.2"

        husbyVersion = "9d026e2ddc"
        lombokVersion = "1.18.18"
    }

    group "com.noahhusby"
    version "$sledgehammerVersion"

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
        maven {
            name = "JitPack"
            url = "https://jitpack.io/"
        }
        maven {
            name = 'papermc-repo'
            url = 'https://papermc.io/repo/repository/maven-public/'
        }
        maven {
            name = 'codemc-snapshots'
            url = 'https://repo.codemc.io/repository/maven-snapshots'
        }
        maven {
            name = "DaPorkchop_"
            url = "https://maven.daporkchop.net/"
        }
    }

    dependencies {
        if (!("common".equalsIgnoreCase(project.name))) {
            compile project(":common")
        }

        // TODO: Remove SQL dependency from HusbyLib and move it to bungee only
        compile "com.github.noahhusby:HusbyLib:$husbyVersion"

        compileOnly "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }
}

dependencies {
    for (Project prj : project.allprojects) {
        if (!"sledgehammer".equalsIgnoreCase(prj.name) && prj.parent.name.equalsIgnoreCase("sledgehammer")) {
            compile project(":" + prj.name)
        }
    }
}

sourceSets {
    main {
        output.dir(versionDir, builtBy: 'generateVersionProperties')
    }
}

task generateVersionProperties {
    doLast {
        def propertiesFile = file "$versionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty("version", sledgehammerVersion.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}

build.dependsOn shadowJar
processResources.dependsOn generateVersionProperties
